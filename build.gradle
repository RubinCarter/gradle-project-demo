//所有子项目的通用配置
subprojects {
    apply plugin: 'java'
    apply plugin: 'maven'
    apply plugin: 'idea'

    version = '1.0'

    //JVM 版本要求
    sourceCompatibility = 1.7
    targetCompatibility = 1.7

    //默认编码格式 UTF-8
    [compileJava, compileTestJava, javadoc]*.options*.encoding = 'UTF-8'

    ext {
        springVersion = '3.2.11.RELEASE'
        hibernateVersion = '4.3.1.Final'
    }

    repositories {
        mavenCentral()
    }

    configurations {
        //所有要忽略的包定义在此
        all*.exclude group: 'commons-httpclient'
        all*.exclude group: 'commons-logging'
        all*.exclude group: 'commons-beanutils', module: 'commons-beanutils'
    }

    dependencies {
        compile(
                "org.springframework:spring-context:$springVersion",
                "org.springframework:spring-orm:$springVersion",
                "org.springframework:spring-tx:$springVersion",
                "org.springframework.data:spring-data-jpa:1.5.2.RELEASE",
                "org.hibernate:hibernate-entitymanager:$hibernateVersion",
                "c3p0:c3p0:0.9.1.2",
                "mysql:mysql-connector-java:5.1.26",
                "org.slf4j:slf4j-nop:1.7.6",
                "commons-fileupload:commons-fileupload:1.3.1",
                "com.fasterxml.jackson.core:jackson-databind:2.3.1"
        )
        dependencies {
            //以来maven中不存在的jar
            ext.jarTree = fileTree(dir: 'libs', include: '**/*.jar')
            ext.rootProjectLibs = new File(rootProject.rootDir, 'libs').getAbsolutePath()
            ext.jarTree += fileTree(dir: rootProjectLibs, include: '**/*.jar')
            compile jarTree
        }

        //测试依赖
        testCompile(
                "org.springframework:spring-test:$springVersion",
                "junit:junit:4.11"
        )
    }

    sourceSets {
        main {
            java {
                srcDirs = ['src/main/java','src/main/groovy']
            }
            resources {
                srcDirs = ['src/main/resources', 'src/main/groovy']
            }
        }
        test {
            java {
                srcDirs = ['src/test/java','src/test/groovy']
            }
            resources {
                srcDirs = ['src/test/resources', 'src/test/groovy']
            }
        }
    }

    idea {
        module {
            downloadJavadoc = false
            downloadSources = false
        }
    }

    //显示当前项目下所有用户 compile 的jar
    task('listJars', description: 'Display all compile jars.') << {
        configurations.compile.each { File file -> println file.name }
    }

}